10 '************************* 
20 '***    THROW-UP       *** 
30 '*** PROGRAM III OF III*** 
40 '*** TO LOAD 24 HI-RES *** 
50 '*** SCREENS INTO 64K  *** 
60 '*** MEM. EXP.         *** 
70 '***  BOB KITCH 6/88   *** 
80 '*************************
90 '
100 '***MAKE INVERSE CHARACTERS IN LINES 5000 AND 5020********* 
3398 ' 
3399 '***SEE IF MEMORY BLOCK LOAD SELECTED. 
3400 A$=CHR$(PEEK(30791)):IF A$="2"THEN GOTO 7000 
3498 ' 
3499 '***PUT UP INTRO MESSAGE. 
3500 GOSUB 5020 
3510 PRINT"NOW IT'S TIME TO HAVE A GOOD           ***THROW-UP***" 
3512 SOUND 8,5;0,3;8,5;0,3;8,5 
3515 PRINT:PRINT"FOUR OPTIONS EXIST":PRINT 
3520 PRINT" <0> FANCY SCREEN MOVE" 
3525 PRINT" <1> PLAIN SCREEN MOVE" 
3530 PRINT" <2> MEMORY BLOCK SAVE" 
3535 PRINT" <3> MEMORY BLOCK LOAD" 
3540 PRINT:INPUT" INDICATE OPTION";A$ 
3545 IF A$="2" THEN GOTO 6000 
3550 IF A$="3" THEN GOTO 7000 
3555 IF (A$<>"1") AND (A$<>"0") THEN GOTO 3500 
3560 GOSUB 5020 
3565 PRINT"FOR A <PICTURE SHOW> DELAY      SELECT 10000 <24 SEC>" 
3570 PRINT"FOR AN ANIMATION DELAY          SELECT 100 <0.25 SEC>" 
3575 PRINT:INPUT"DELAY";D 
3580 IF D<0 THEN GOTO 3500 
3590 IF A$="0" THEN GOTO 4000 
3598 ' 
3599 '***SET UP M/L ROUTINE FOR PAGING. 
3600 POKE -16719,112              :'***MSB OF DEST TO 70H. 
3610 MS%=-16722                   :'***MSB OF SOURCE PG. 0BEAEH. 
3620 POKE 30862,171:POKE 30863,190:'***ENSURE USR VECTOR SET. 
3698 ' 
3699 '***MAIN PLAIN DISPLAY ROUTINE. 
3700 MODE(1) 
3710 FOR I%=1 TO 3 
3720    OUT 127,I% 
3730    FOR J%=192 TO 255 STEP 8 
3740       POKE MS%,J% 
3750       K%=USR(0) 
3760          FOR K%=0 TO D 
3770          NEXT K% 
3780    NEXT J% 
3790 NEXT I% 
3800 GOTO 3710 
3998 ' 
3999 '***MAIN FANCY DISPLAY ROUTINE. 
4000 MODE(1):POKE 30863,188 : '***MSB 0BCH. 
4010 FOR I%=1 TO 3 
4020 OUT 127,I% 
4030    FOR J%=3 TO 26 STEP 3 
4040       POKE 30862,J% 
4050       K%=USR(0) 
4060          FOR K%=0 TO D 
4070          NEXT K% 
4080    NEXT J% 
4090 NEXT I% 
4100 GOTO 4010 
4998 ' 
4999 '***SUBROUTINE FOR NEW SCREEN. 
5000 PRINT@480, "PRESS ANY KEY TO CONTINUE";
5010 A$=INKEY$:A$=INKEY$:IF A$="" THEN GOTO 5010 
5020 CLS: PRINT@12, "THROW-UP": PRINT: PRINT: RETURN
5997 ' 
5999 '***MEMORY BLOCK SAVE ROUTINE. 
6000 CLS:PRINT@7,"MEMORY BLOCK SAVE" 
6010 PRINT@64,"PUT IN NEW INITIALIZED DISK" 
6020 INPUT"PRESS ANY KEY TO CONTINUE";A$ 
6030 PRINT@128,"SAVING BLOCK 1" 
6040 OUT 127,1:BSAVE"BLOCK1",C000,FFFE 
6050 PRINT@192,"SAVING BLOCK 2" 
6060 OUT 127,2:BSAVE"BLOCK2",C000,FFFE 
6070 PRINT@256,"SAVING BLOCK 3" 
6080 OUT 127,3:BSAVE"BLOCK3",C000,FFFE 
6090 GOTO 3500 
6997 ' 
6999 '***MEMORY BLOCK LOAD ROUTINE. 
7000 CLS:PRINT@7,"MEMORY BLOCK LOAD":POKE 30791,0 
7010 PRINT@64,"PUT IN DISK CONTAINING 3 MEM. BLOCKS" 
7020 INPUT"PRESS ANY KEY TO CONTINUE";A$ 
7030 PRINT@192,"LOADING BLOCK 1" 
7040 OUT 127,1:BLOAD"BLOCK 1" 
7050 PRINT@256,"LOADING BLOCK 2" 
7060 OUT 127,2:BLOAD"BLOCK2" 
7070 PRINT@320,"LOADING BLOCK 3" 
7080 OUT 127,3:BLOAD"BLOCK3" 
7090 GOTO 3500 
9998 ' 
9999 '***UPDATE DISK FILE. 
10000 ERA"THROWUP":SAVE"THROWUP" 
20000 END 
