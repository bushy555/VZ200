1 '****************************
2 '*** SCREEN MODE MOVER ***
3 '*** A SET OF SUBROUTINES ***
4 '*** TO SWITCH BETWEEN ***
5 '*** HI & LO RES ***
6 '*** BY ***
7 '*** BOB KITCH ***
8 '*** 6/87 ***
9 '****************************
10 '
15 'THIS SET OF SUBROUTINES IS USED IN BRUCE'S ART GALLERY.
2998 '
2999 '***INITIALIZATION -CALL AT START OF PROGRAM.
3000 '***DECLARE ALL VARIABLES AS INTEGERS.
3001 M1%=0:L1%=0:TM%=0:MS%=0:LS%=0:MR%=0:LR%=0
3002 ID%=0:SS%=0:DS%=0:ZS%=0:MV%=0:LV%=0:SU%=0
3003 '
3005 M1%=PEEK(30898):L1%=PEEK(30897) :'***FIND TOM,
3010 TM%=M1%*256+L1% :'***TOM.
3020 TM%=TM%-2400 :'***RESERVE 2.4K BUFFER.
3030 MS%=INT(TM%/256):LS%=TM%-MS%*256 :'***NEW TOM.
3040 POKE 30898,MS%:POKE 30897,LS% :'***SET NEW TOM.
3050 CLEAR 300 :'***RESET POINTERS.
3060 TM%=PEEK(30898)*256+PEEK(30898) :'***NEW TOM.
3070 TM%=TM%+1 :'***START ADDR. BUFFER.
3080 MR%=INT(TM%/256):LR%=TM%-MR%*256 :'***START POINTERS.
3090 FOR MS%=TM% TO TM%+13 :'***LOAD IN M/C.
3100 READ LS%:ID%=MS% :'***SET UP ADDR.
3110 IF ID%>32767 THEN ID%=ID%-65536:'***CONV. TO S.INT.
3120 POKE ID%,LS% :'***POKE IN MOVE CODE.
3130 NEXT MS%
3140 SS%=TM%+1 :'***ADDR. SOURCE.
3150 IF SS%>32767 THEN SS%=SS%-65536 :'***CONV. TO S.INT.
3160 DS%=TM%+4 :'***ADDR. DESTIN.
3170 IF DS%>32767 THEN DS%=DS%-65536 :'***CONV. TO S.INT.
3180 ZS%=TM%+9 :'***ADDR. 2K BUFF.
3190 IF ZS%>32767 THEN ZS%=ZS%-65536 :'***CONV. TO S.INT.
3200 TM%=TM%+30 :'***START OF SCRN BUFF.
3210 MS%=INT(TM%/256):LS%=TM%-MS%*256 :'***START OF 2K BUFF.
3220 MV%=112:LV%=0 :'***START OF VIDEO RAM.
3230 RETURN
3298'
3299 '***DESTRUCTIVE BLOCK FILL ROUTINE.
3300 DATA 033,255,255 :'LD HL,SOURCE.
3310 DATA 017,255,255 :'LD DE,DESTINATION
3320 DATA 001,000,008 :'LD BC,SIZE
3330 DATA 054,000 :'LD (HL),CHARACTER
3340 DATA 237,176 :'LDIR
3350 DATA 201 :'RET
3998 '
3999 '***CLEAR BUFFER ROUTINE.
4000 POKE 30863,MR%:POKE 30862,LR% :'***SET USR() POINTERS.
4010 POKE SS%,LS%:POKE SS%+1,MS% :'***SOURCE ADDR.
4020 M1%=MS%:L1%=LS%+1 :'***CALC SOURCE+1.
4030 IF L1%>255 THEN L1%=0:M1%=M1%+1 :'***CHECK RANGE.
4040 POKE DS%,L1%:POKE DS%+1,M1% :'***DESTINATION ADDR.
4050 SU%=USR(0) :'***SET 2K BUFFER.
4060 POKE ZS%,0:POKE ZS%+1,0 :'***INSERT NOP'S
4070 RETURN
4998 '
4999 '***MOVE 2K SCREEN BUFFER TO TOM BUFFER.
5000 POKE 30863,MR%:POKE 30862,LR% :'***SET USR() POINTERS.
5010 POKE SS%,LV%:POKE SS%+1,MV% :'***SET SOURCE ADDR.
5020 POKE DS%,LS%:POKE DS%+1,MS% :'***SET DESTINATION ADDR.
5030 SU%=USR(0) :'***MOVE VRAM TO BUFFER.
5040 RETURN
5998 '
5999 '***MOVE TOM BUFFER INTO 2K VIDEO RAM ...
6000 POKE 30863,MR%:POKE 30862,LR% :'***SET USR() POINTERS.
6010 MODE(1) :'***HI-RES MODE.
6020 POKE SS%,LS%:POKE SS%+1,MS% :'***SET SOURCE ADDR.
6030 POKE DS%,LV%:POKE DS%+1,MV% :'***SET DESTINATION ADDR.
6040 SU%=USR(0) :'***MOVE BUFFER TO VRAM.
6050 RETURN
7000 END