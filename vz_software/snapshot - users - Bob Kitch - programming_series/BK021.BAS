10 '**********************************
20 '***PLOT A SET OF UP TO 20 LINES***
30 '***USING THE HI-RES SCREEN. ***
40 '***R.B.KITCH 22/10/85 ***
50 '**********************************
90 '
100 '***DIMENSION STORAGE VECTORS X% & Y%.
110 DIM X%(20),Y%(20):CLS:'***VECTORS TO HOLD END CO-ORDS.
115 '
120 '***ACCEPT INPUT AND CHECK.
130 INPUT"HOW MANY LINES - MAX 20 ";LN%
140 IF LN%<1 OR LN%>20 THEN GOTO 130
150 FOR I%=0 TO LN% '***LOOP FOR LN%+1 X-Y POINTS.
160 INPUT"ENTER X-VAL 0-127 ';X%(I%)
170 IF X%(I%)<0 OR X%(I%)>127 THEN GOTO 160:'***CHECK ON SCRN
180 INPUT"ENTER Y-VAL 0-63 ';Y%(I%)
190 IF Y%(I%)<0 OR Y%(I%)> 63 THEN GOTO 180:'***CHECK ON SCRN
200 NEXT I%
290 '
300 '***SET UP SCREEN AND MAIN PLOTTING LOOP.
310 MODE(1) :'***SWITCH SCREEN TO HI-RES.
320 FOR I%=0 TO LN%-1 :'***ASSIGN MAIN LOOP FOR LN% LINES.
330 X1%=X%(I%):X2%=X%(I%+1) :'***ASSIGN END POINTS TO TEMP VAR
340 Y1%=Y%(I%):Y2%=Y%(I%+1) :'***ASSIGN END POINTS TO TEMP VAR
350 '***ARE THE POINTS THE SAME?
360 IF X1%<>X2% OR Y1%<>Y2% THEN GOTO 410
370 SET(X1%,Y1%) :'***END POINTS ARE THE SAME SO PLOT.
380 GOTO 710
390 '
400 ' ***CALCULATE X ANDY DIFFERENCE.
410 DX%=X2%-X1%:DY%=Y2%-Y1% : '***CHANGE IN X & Y DIRECTIONS.
420 ' ***SEE WHICH IS LARGER.
430 IF ABS(DX%)>ABS(DY%) THEN GOTO 610
490 '
500 ' ***INCREMENT IY OR ALONG Y-AXIS.
510 YS%=SGN(DY%):DG=DX%/DY% :'***SIGN OF STEP AND GRADIENT.
520 XO=X1%+0.5 :'***X-AXIS OFFSET.
530 FOR IY%=Y1% TO Y2% STEP YS%:'***INITIALIZE LOOP.
540 TP=(IY%-Y1%)*DG+XO :'***TEMP REAL X-VALUE.
550 IX%=INT(TP) :'***INTEGER X-VALUE.
560 SET(IX%,IY%)
570 NEXT IY%
580 GOTO 710 :'***PICK UP ANOTHER LINE.
600 ' ***INCREMENT IX OR ALONG X-AXIS.
610 XS%=SGN(DX%):DG=DY%/DX% :'***SIGN OF STEP AND GRADIENT.
620 YO=Y1%+0.5 :'***Y-AXIS OFFSET.
630 FOR IX%=X1% TO X2% STEP XS%:'***INITIALIZE LOOP.
640 TP=(IX%-X1%)*DG+YO :'***TEMP REAL Y-VALUE.
650 IY%=INT(TP) :'***INTEGER Y-VALUE.
660 SET(IX%,IY%)
670 NEXT IX%
690 '
700 ' ***END LOOP FOR LINE.
710 NEXT I%:SOUND 28,6 :'***END LOOP.
720 AN$=""
730 AN$=INKEY$:AN$=INKEY$ :'***PAUSE FOR ANY KEYSTROKE.
740 IF AN$="" THEN GOTO 730
790 '
800 '***GO AGAIN?
810 CLS:PRINT" (E) TO EXIT" :'***SCREEN MESSAGE OR MENU.
820 PRINT" (P) TO PLOT AGAIN"
830 PRINT" (N) FOR NEW POINTS":PRINT
840 INPUT AN$ :'***ACCEPT RESPONSE.
850 AN$=LEFT$(AN$,1) :'***CLEAN IT UP.
860 IF AN$="E" THEN STOP :'***LOGICAL END TO FROG.RAM.
870 IF AN$="P" THEN GOTO 310 :'***GO BACK AND PLOT AGAIN.
880 IF AN$="N" THEN GOTO 130 :'***GO BACK FOR MORE INPUT.
890 GOTO 810 :'***WRONG RESPONSE.
900 END
