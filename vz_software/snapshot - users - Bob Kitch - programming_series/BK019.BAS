1 '*************************** 
2 '***  INTERRUPT DRIVEN   *** 
3 '***  REAL TIME CLOCK    *** 
4 '***  FOR VZ COMPUTER    *** 
5 '***    BY BOB KITCH     *** 
6 '***TIME KEEPING ROUTINE *** 
7 '***    IS COMPLETELY    *** 
8 '***     RELOCATABLE     *** 
9 '*************************** 
10 ' 
20 GOTO 100 
26 ' 
27 '***USEFUL NUMERIC LOADING SUB-ROUTINES. 
28 ' 
29 '***CONVERT UNSIGNED TO SIGNED DECIMAL -PASSED IN UD & SD%. 
30 IF UD>32767 THEN SD%=INT(UD-65536) ELSE SD%=INT(UD) 
35 RETURN 
38 ' 
39 '***CONVERT UNSIGNED DECIMAL TO MSB & LSB -IN UD, MS%, LS%. 
40 MS%=INT(UD/256):LS%=INT(UD-256*MS%) 
45 RETURN 
48 ' 
49 '***CONVERT SIGNED TO UNSIGNED DECIMAL -PASSED IN SD% & UD. 
50 IF SD%<0 THEN UD=SD%+65536 ELSE UD=SD% 
55 RETURN 
98 ' 
99 '***PUT UP INTRO MESSAGE. 
100 CLS:PRINT@4,"REAL TIME CLOCK ROUTINE" 
110 PRINT@197,"LOADING MACHINE CODE" 
199 ' 
200 '***LOWER TOM TO ENABLE ROUTINE TO BE STORED. 
210 TM=(256*PEEK(30898)+PEEK(30897))-100: '***RESERVE 99 BYTES. 
220 UD=TM:GOSUB 40:POKE 30898,MS%:POKE 30897,LS% 
230 CLEAR 100:                            '***RESET POINTERS. 
240 S%=0:M%=0:H%=0:SA%=0:MA%=0:HA%=0 
250 UD=0:TM=0:AD=0:SD%=0:MS%=0:LS%=0:     '***INITIALIZE STORAGE 
260 I%=0:J%=0:DV%=0:CS%=0:                '***BEFORE DIMEN'ING. 
270 DIM VA%(2,23) :'***INITIALIZE ARRAY. 
280 TM=256*PEEK(30898)+PEEK(30897):       '***NEW TOM. 
299 ' 
300 '***READ IN TIME KEEPING ROUTINE. 
310 FOR AD=TM+1 TO TM+77 
320    UD=AD:GOSUB 30:READ DV%:POKE SD%,DV% 
330    CS%=CS%+DV% 
340 NEXT AD 
350 IF CS%<>8170 THEN PRINT@293,"CHECKSUM NOT CORRECT";CS%:STOP 
399 ' 
400 '***INITIALIZE POINTERS IN VA%(). 
410 FOR I%=0 TO 23 
420    READ VA%(0,I%):UQ=TM+VA%(0,I%):GOSUB 30:GOSUB 40 
430    VA%(0,I%)=SD:VA%(1,I%)=LS%:VA%(2,I%)=MS% 
440 NEXT I% 
499 ' 
500 '***MODIFY DUMMY ADDRESSES IN TIME KEEPING ROUTINE. 
510 POKE VA%(0,4),VA%(1,0):POKE VA%(0,5),VA%(2,0): '***<KOUNT> 
520 FOR I%= 1 TO 3                    :'***<SEC><MIN><HOUR> 
530    FOR J%=I%*6 TO I%*6+4 STEP 2   :'***3 OCC'ENCES EACH 
540       POKE VA%(0,J%),VA%(1,I%):POKE VA%(0,J%+1),VA%(2,I%) 
550    NEXT J% 
560 NEXT I% 
570 SA%=VA%(0,1):MA%=VA%(0,2):HA%=VA%(0,3):'ADDR FOR SEC.MIN.HR 
599 ' 
600 '***SET UP JUMP ADDRESS IN INTERRUPT VECTOR. 
610 UD=TM+1:GOSUB 40 
620 POKE 30846,LS%:POKE 30847,MS% :'***BYTES 2 & 3. 
630 'GO TO 20000        : '***DEBUG JUMP TO PRINTER LISTING. 
699 ' 
700 '***SET TIME. 
710 PRINT@293,"***ENTER CURRENT TIME." 
720 PRINT@325,"INPUT HOURS";:INPUT DV% 
730 IF DV%<1 OR DV%>12 THEN GOTO 720 ELSE POKE HA%,DV% 
740 PRINT@389,"INPUT MINS ";:INPUT DV% 
750 IF DV%<0 OR DV%>59 THEN GOTO 740 ELSE POKE MA%,DV% 
799 ' 
800 '***STEAL INTERRUPT VECTOR. 
810 POKE 30845,195     :'***JUMP TO TIME ROUTINE. 
899 ' 
900 '***DISPLAY TIME ON BOTTOM LINE OF SCREEN. 
910 FOR I%=29120 TO 29151 
920    POKE I%,109 
930 NEXT I% 
940 T$="TIME ##:##:##" :'***DISPLAY TEMPLATE. 
950 'CLS               :'***REMOVE REM IF REQ'D. 
960 S%=PEEK(SA%):M%=PEEK(MA%):H%=PEEK(HA%) 
970 PRINT@484,USING T$;H%,M%,S%; 
980 GOTO 960 
999 ' 
2000 '***TIME KEEPING ROUTINE*** 
2010 '***ALL ADDRESSES SET TO 255 ARE DUMMY PARAMETERS AND ARE 
2020 '***RESET FROM BASIC. 
2030 '***REGISTERS USED AF, BC, HL. 
3000 ' 
3010 '                       ***COUNT DOWN SECONDS. 
3020 DATA 033,255,255:'         LD HL,KOUNT 
3030 DATA 053:'                 DEC(HL) 
3040 DATA 192:'                 RET NZ 
3050 '                       ***SECONDS ROUTINE. 
3060 DATA 054,050:'             LD(HL),50 
3070 DATA 058,255,255:'         LD A,(SEC) 
3080 DATA 060:'                 INC A 
3090 DATA 254,060:'             CP 60 
3100 DATA 040,005:'             JR Z,L1 
3110 DATA 050,255,255:'         LD(SEC),A 
3120 DATA 024,056:'             JR L4 
3130 '                       ***MINUTES ROUTINE. 
3140 DATA 033,255,255:'      L1 LD HL,SEC 
3150 DATA 054,000:'             LD(HL),0 
3160 DATA 058,255,255:'         LD A,(MIN) 
3170 DATA 060:'                 INC A 
3180 DATA 254,060:'             CP 60 
3190 DATA 040,005 :'            JR Z,L2 
3200 DATA 050,255,255:'         LD(MIN),A 
3210 DATA 024,038:'             JR L4 
3220 '                       ***HOUR ROUTINE. 
3230 DATA 033,255,255:'      L2 LD HL,MIN 
3240 DATA 054,000 :'            LD(HL),0 
3250 DATA 001,000,001:'         LD BC,256 
3260 DATA 033,042,000:'         LD HL,42 
3270 DATA 205,092,052:'         CALL 345CH 
3280 DATA 058,255,255:'         LD A,(HOUR) 
3290 DATA 060:'                 INC A 
3300 DATA 254,013:'             CP 13 
3310 DATA 040,005:'             JR Z,L3 
3320 DATA 050,255,255:'         LD(HOUR),A 
3330 DATA 024,011:'             JR 14 
3340 '                       ***HOUR RESET ROUTINE. 
3350 DATA 033,255,255:'      L3 LD H1,HOUR 
3360 DATA 054,001:'             LD(HL),1 
3370 DATA 024,004:'             JR L4 
3380 DATA 060:'                 DS KOUNT 
3390 DATA 000:'                 DS SEC 
3400 DATA 000:'                 DS MIN 
3410 DATA 000:'                 DS HOUR 
3420 '                       ***SCREEN UPDATE ROUTINE HERE. 
3430 DATA 201:'              L4 RET 
3999 ' 
4000 '***OFFSETS FOR PARAMETER STORAGE ABOVE ROUTINE. 
4010 DATA 73:'                KOUNT 
4020 DATA 74:'                SEC 
4030 DATA 75:'                MIN 
4040 DATA 76:'                HOUR 
4200 '***OFFSETS FOR PARAMETERS IN MACHINE CODE ROUTINE. 
4210 DATA 2,3:'              <KOUNT> 
4220 DATA 9,10:'             <SEC> 
4230 DATA 17,18:'            <SEC> 
4240 DATA 22,23:'            <SEC> 
4250 DATA 27,28:'            <MIN> 
4260 DATA 35,36:'            <MIN> 
4270 DATA 40,41:'            <MIN> 
4280 DATA 54,55:'            <HOUR> 
4290 DATA 62,63:'            <HOUR> 
4300 DATA 67,68:'            <HOUR> 
4310 STOP 
4999 ' 
9999 '***UPDATE DISK FILE. 
10000 ERA"RTCLOCK":SAVE"RTCLOCK" 
10010 STOP 
19999 ' 
20000 '***DEBUG DUMP TO PRINTER. 
20010 '***TO ACTIVATE, TAKE OUT REMARK IN LINE#630. 
20020 LPRINT"DEBUG DUMP FROM REAL TIME CL0CK":LPRINT 
20030 LPRINT"TOM=",TM:LPRINT 
20040 LPRINT"INTERRUPT VECTOR",PEEK(30846),PEEK(30847):LPRINT 
20050 LPRINT"PARAMETERS" 
20060 FOR I%=0 TO 3 
20070    LPRINT I%,VA%(0,I%),VA%(1,I%),VA%(2,I%) 
20080 NEXT I%:LPRINT 	
20090 LPRINT"POINTERS IN VA()" 
20100 FOR I%=4 TO 23 
20110    LPRINT I%,VA%(0,I%),VA%(1,I%),VA%(2,I%) 
20120 NEXT I%:LPRINT 
30000 END 
