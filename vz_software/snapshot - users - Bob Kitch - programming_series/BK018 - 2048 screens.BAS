5 'LISTING 5
10 '************************* 
20 '***  2048 VZ SCREENS  *** 
30 '***    VERSION 1.3    *** 
40 '***  R.B.K. 18/5/86   *** 
50 '************************* 
88 ' 
89 '***FIND TOP OF MEMORY AND PROTECT 20 BYTES BY LOWERING TOM. 
90 IF PEEK (30791)=77 THEN GOTO 500 :'7847H AS LOAD CHECK. 
100 M1=PEEK(30898):L1=PEEK(30897)   :'TOM POINTERS. 
110 TM=M1*256+L1-21                 :'PROTECT 20 BYTES. 
120 MS=INT(TM/256):LS=TM-MS*256     :'NEW TOM POINTERS. 
130 POKE 30898,MS:POKE 30897,LS     :'SET NEW TOM. 
140 CLEAR 50                        :'RESET ALL POINTERS. 
150 'THIS ACTION DESTROYS THE VLT-ALL VARIABLES MUST BE RENEWED. 
160 M1=PEEK(30898):L1=PEEK(30897)   :'NEW TOM POINTERS. 
170 ST=M1*256+L1+1                  :'NEXT ADDR IN RESERVED 
180 MS=INT(ST/256):LS=ST-256*MS     :'START OF ROUTINE. 
190 IF ST>32767 THEN ST=ST-65536    :'CONVERT TO SIGNED INT 
198 ' 
199 '***LOAD MACHINE CODE INTO PROTECTED TOM AND SET USR POINTER 
200 CLS:PRINT@230,"LOADING MACHINE CODE" 
210 CS%=0 :'CHECK SUM COUNTER. 
220 FOR ID=ST TO ST+15 :'LOAD 16 BYTES TO TOM. 
230    READ VL%:POKE ID,VL%:CS%=CS%+VL% :'LOAD M/L INTO PROTECT 
240 NEXT ID 
250 IF CS%<>1483 THEN GOTO 1000   :'WRONG DATA. 
260 POKE 30863,MS:POKE 30862,LS   :'SET USR POINTERS. 
270 POKE 30791,77                 :'SET LOAD CHECK. 
399 ' 
400 '***Z-80 BLOCK MOVE ROUTINE. 
410 DATA 058,033,121  :'LD A,(7921H) PICK UP VALUE FROM USR(). 
420 DATA 033,000,112  :'LD HL,7000H (#28672D START VIDEO RAM) 
430 DATA 017,001,112  :'LD DE,7001H (#28673D NEXT) 
440 DATA 001,255,007  :'LD BC,07FFH (#2047D SIZE OF VIDEO RAM) 
450 DATA 119          :'LD (HL),A 
460 DATA 237,176      :'LDIR (BLOCK LOAD COMMAND) 
470 DATA 201          :'RET 
498 ' 
499 '***INITIALIZE DELAYS - CONTROL SPEED OF EXECUTION BY D%. 
500 T%=0        :'TONE IS REST. (0-31) 
510 D%=4        :'DELAY IS SHORT(1-9) 
520 P%=30744    :'SET UP SCREEN MODE. 
594 ' 
595 '***THIS LOOP DOES A LOT OF WORK. 
596 '***IT LOADS ALL VALUES INTO THE A-REG FOR VIEWING. 
597 '***IT SWAPS BETWEEN HI-AND LO-RES SCREEN MODES. 
598 '***IT SWITCHES BETWEEN NORMAL AND INVERSE SCREEN TYPES. 
599 '***IT SWITCHES BETWEEN GREEN AND ORANGE BACKGROUNDS. 
600 FOR A%=0 TO 255 :'ALL VALUES PUT INTO A=REG. 
610    MODE(0):CLS:PRINT@234,"VALUE = ";A%:SOUND T%,D% 
620    POKE P%,0 
625    X=USR(A%) 
630    COLOR,0:SOUND T%,D% 
640    COLOR,1:SOUND T%,D% 
650    POKE P%,1 
655    X=USR(A%) 
660    COLOR,0:SOUND T%,D% 
670    COLOR,1:SOUND T%,D% 
680 ' 
700 ' HI-RES SCREENS. 
710    MODE(1) 
720    POKE P%,0 
725    X=USR(A%) 
730    COLOR,0:SOUND T%,D% 
740    COLOR,1:SOUND T%,D% 
750    POKE P%,1 
755    X=USR(A%) 
760    COLOR,0:SOUND T%,D% 
770    COLOR,1:SOUND T%,D% 
780    POKE P%,0:COLOR,0 
790 NEXT A% 
800 GOTO 600 
998 ' 
999 '***WRONG DATA MESSAGE. 
1000 CLS:PRINT@230,"ERROR IN DATA ";CS% 
1010 END 
