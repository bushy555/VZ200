5 '*** VZ-300 INSTANT COLOR ***
6 '*** APC AUGUST '85 ***
7 '*** R.B.KITCH 18.5.86 ***
8 '
9 '***LOAD MACHINE CODE.***
10 FOR I=-28687 TO -28674
20 READ A:POKE I,A
30 NEXT I
39 '
40 DATA 33,0,112 :'LD HL,7000H (#28672D START VIDEO RAM)
41 DATA 17,1,112 :'LD DE,7001H (#28673D NEXT)
42 DATA 1,255,7 :'LD BC,07FFH (#2047D SIZE OF VIDEO RAM)
43 DATA 54,85 :'LD (HL),55H (#85D YELLOW OR CHAR "U")
44 DATA 237,176 :'LDIR (BLOCK LOAD COMMAND)
45 DATA 201 :'RET
46 '
49 '***INITIALIZE USR() TO ADDRESS 8FF1H OR #-28687D***
50 POKE 30862,241:POKE 30863,143
60 '***INITIALIZE DELAYS.***
61 T=0 :'***TONE 0 IS REST.
62 D=9 :'***DURATION 9 IS LONG.
63 '***SET UP DEMO LOOP.***
64 FOR I=0 TO 255
65 POKE -28677,I :'***OVERWRITE WITH NEW CHARACTER.***
66 ' ***SCREEN MESSAGE.***
67 MODE(0):PRINT@234," CHAR = ";I:SOUND T,D
68 X=USR<0> :'***FILL 2K VIDEO RAM WITH CHAR.***
69 ' ***LO-RES GREEN BACKGROUND.***
70 COLOR,0:SOUND T,D
79 ' ***LO-RES ORANGE BACKGROUND.***
80 COLOR,1:SOUND T,D
89 ' ***HI-RES COLOR SET 1***
90 MODE(1):X=USR<0> :'***FILL AGAIN AFTER RESET.***
91 COLOR,0:SOUND T,D
99 ' ***HI-RES COLOR SET 2***
100 COLOR,1:SOUND T,D
110 NEXT I
120 STOP:END