


1 '**************************
2 '***  TONE GENERATOR    ***
3 '***   BY R. B.KITCH    ***
4 '***     17/4/87        ***
5 '***  REF: VDU 3/16     ***
6 '**************************
9 '
10 CLS:PRINT@8,"TONE GENERATOR":PRINT@197,"LOADING MACHINE CODE"
11 GOTO 110
19 '
20 ' ***CONVERT UNSIGNED TO SIGNED DECIMAL - PASSED IN UD & SD%.
30 IF UD>32767 THEN SD%=INT(UD-65536) ELSE SD%=INT(UD)
35 RETURN
39 '
40 ' ***CONVERT UNSIGNED DECIMAL TO MSB & LSB - IN UD,MS%,LS%.
50 MS%=INT(UD/256):LS%=INT(UD-256*MS%):RETURN
59 
60 '***CONVERT SIGNED TO UNSIGNED DECIMAL - PASSED IN SD% & UD.
70 IF SD%<0 THEN UD=SD%+65536 ELSE UD=SD%
80 RETURN
99 '
100 ' ***LOWER TOM TO SAVE ROUTINE.
110 TM=256*PEEK(30898)+PEEK(30897)-100   :'***RESERVE 99 BYTES.
120 UD=TM:GOSUB 50:POKE 30898,MS%:POKE 30897,LS%
130 CLEAR 100                           :'***RESETS ALL VARIABLES.
140 UD=0:TM=0:AD=0:SD%=0:MS%=0:LS%=0     :'***INITIALIZE STORAGE.
150 I%=0:DV%=0
160 DIM VA%(2,23),PA%(4),MS$(4)          :'***INITIALIZE ARRAYS.
170 TM=256*PEEK(30898)+PEEK(30897)       :'***TOM.
199 '
200 '***READ IN SOUND ROUTINE.
210 FOR AD=TM+1 TO TM+60                 :'***LOAD 60 BYTES.
220    UD=AD:GOSUB 30:READ DV%:POKE SD%,DV%
230 NEXT AD
240 UD=TM+1:GOSUB 50                    :'***SET USR POINTERS.
250 POKE 30863,MS%:POKE 30862,LS%
299 '
300 ' ***INITIALIZE POINTERS IN VA%().
310 FOR I%=0 TO 23
320 READ VA%(0,I%):UD=TM+VA%(0,I%):GOSUB 30:GOSUB 50
330    VA%(0,I%)=SD%:VA%(1,I%)=LS%:VA%(2,I%)=MS%
340 NEXT I%
399 '
400 '***POKE STORAGE LOCATIONS INTO ROUTINE.
410 POKE VA%(0,10),VA%(1,4): POKE VA%(0,11),VA%(2,4)
420 POKE VA%(0,12),VA%(1,6): POKE VA%(0,13),VA%(2,6)
430 POKE VA%(0,14),VA%(1,8): POKE VA%(0,15),VA%(2,8)
440 POKE VA%(0,16),VA%(1,0): POKE VA%(0,17),VA%(2,0)
450 POKE VA%(0,18),VA%(1,0): POKE VA%(0,19),VA%(2,0)
460 POKE VA%(0,20),VA%(1,2): POKE VA%(0,21),VA%(2,2)
470 POKE VA%(0,22),VA%(1,2): POKE VA%(0,23),VA%(2,2)
499 '
500 ' ***LOAD SCREEN MESSAGES.
510 MS$(0)="RANGE OF+/-32767"+CHR$(10)+CHR$(13)+"   ON INTERVAL"
520 MS$(1)="  OFF INTERVAL"
530 MS$(2)="  ON INCREMENT"
540 MS$(3)=" OFF INCREMENT"
550 MS$(4)="   TONE LENGTH"
599 '
600 '***FIND OUT PARAMETERS.
610 CLS:PRINT@8,"TONE GENERATOR": PRINT
620 FOR I%=0 TO 4
630    PRINT MS$(I%);:INPUT PA%(I%):SD%=PA%(I%)
640    GOSUB 70:GOSUB 50
650    POKE VA%(0,2*I%),LS%: POKE VA%(0,2*I%+1),MS%
660 NEXT I%
670 'GOTO 20000                :'***DEBUG JUMP.
699 '
700 '***RUN TONE ROUTINE.
710 AD=USR(0)
799'
800 '***GO AGAIN?
810 PRINT:INPUT"ANOTHER TONE (Y/N)";A$:IF A$="Y" THEN GOTO 610
899 ' 
900 '***RESET TOM ON EXIT.
910 TM=TM+100                  :'***ORIGINAL TOM.
920 UD=TM :GOSUB 50:POKE30898,MS%:POKE30897,LS%
930 CLEAR 50
940 STOP
999 '
1000 '***SOUND ROUTINE - VALUES OF 0 ARE RESET LATER.
1001 '***ALSO SETS CASSETTE PORT ON BITS 1&2.
1002 '***REGISTERS USED AF, BC, DE, HL & HL'.
1003 '***LENGTH 60 BYTES. NO STACK USED.
1010 DATA 237,75,0,0       :'     LD BC,(ONINC)
1020 DATA 237,91,0,0       :'     LD DE,(OFINC)
1030 DATA 217              :'     EXX
1040 DATA 42,0,0           :'     LD HL',(TONDUR)
1050 DATA 217              :'T1   EXX
1060 DATA 62,36            :'     LD A,00100100B
1070 DATA 42,0,0           :'     LD HL,(ONDUR)
1080 DATA 9                :'     ADD HL,BC
1090 DATA 34,0,0           :'     LD (ONDUR),HL
1100 DATA 50,00,104        :'     LD (6800),A
1110 DATA 43               :' T2  DEC HL
1120 DATA 125              :'     LD A,L
1130 DATA 180              :'     OR H
1140 DATA 32,251           :'     JR NZ,T2
1150 DATA 62,3             :'     LD A,00000011B
1160 DATA 42,0,0           :'     LD HL,(OFDUR)
1170 DATA 25               :'     ADD HL,DE
1180 DATA 34,0,0           :'     LD (OFDUR),HL
1190 DATA 50,00,104        :'     LD (6800),A
1200 DATA 43               :'T3   DEC HL
1210 DATA 125              :'     LD A,L
1220 DATA 180              :'     OR H
1230 DATA 32,251           :'     JR NZ,T3
1240 DATA 217              :'     EXX
1250 DATA 43               :'     DEC HL'
1260 DATA 125              :'     LD A,L'
1270 DATA 180              :'     OR H'
1280 DATA 32,215           :'     JR NZ,T1
1290 DATA 217              :'     EXX
1300 DATA 62,00            :'     LD A,00000000B
1310 DATA 50,00,104        :'     LD(6800),A
1320 DATA 201              :'     RET
1699 '
1700 '***OFFSETS FOR PARAMETER STORAGE ABOVE ROUTINE.
1710 DATA 81,82     :'ON INTERVAL   - PA%(0)
1720 DATA 83,84     :'OFF INTERVAL  - PA%(1)
1730 DATA 85,86     :'ON INCREMENT  - PA%(2)
1740 DATA 87,88     :'OFF INCREMENT - PA%(3)
1750 DATA 89,90     :'TONE DURATION - PA%(4)
1759 '
1760 '***OFFSETS FOR PARAMETERS IN MACHINE CODE ROUTINE.
1770 DATA 3,4      :'ONINC
1780 DATA 7,8      :'OFINC
1790 DATA 11,12    :'TONDUR
1800 DATA 17,18    :'ONDUR
1810 DATA 21,22    :'ONDUR
1820 DATA 34,35    :'OFDUR
1830 DATA 38,39    :'OFDUR
1840 STOP
9999'
10000 '***UPDATE DISK FILE.
10010 CLS:PRINT@200,"ERASING FILE":ERA"TONEGEN"
10020 PRINT@200,"SAVING FILE ":SAVE"TONEGEN"
10030 STOP
10000 '
20000 '***PRINTER DUMP FOR DEBUGGING.
20001 '***TO ACTIVATE TAKE OUT REMARK IN LINE #670.
20010 LPRINT"DEBUG OUTPUT FROM TONEGEN":LPRINT
20020 LPRINT"TOM =",TM:LPRINT
20030 LPRINT"USR() ADDR. =",PEEK(30862),PEEK(30863):LPRINT
20040 LPRINT"PARAMETERS"
20050 FOR I%=0 TO 4
20060    LPRINT MS$(I%),I%,PA%(I%)
20070 NEXT I%:LPRINT
20080    LPRINT"POINTERS IN VA%()"
20090 FOR I%=0 TO 23
20100    LPRINT I%,VA%(0,I%),VA%(1,I%),VA%(2,I%)
20110 NEXT I%
20120 END

