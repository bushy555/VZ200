

10 '**********************
20 '**   SOUND EFFECTS  **
30 '**     LE VZ #10    **
40 '**   R.B.K. 30/1/86 **
50 '**  ALTERED 26/8/90 **
60 '**RBK,ANDREW WILLOWS**
70 '**KEN CLARKE (NZ)   **
80 '**********************
90 '*****POKE IN M/L ROUTINE*****
100 FOR T%=-28687 TO -28676 :'POKE INTO 8FF1H TO 8FFCH.
110    READ D%:POKE T%,D%
120 NEXT T%
121 PT%=-28685                 :'ADDR. FOR PITCH (L-REG)
122 DR%=-28682                 :'ADDR. FOR DURATION (C- REG)
130 DATA 229                   :'   PUSH HL
140 DATA 033,160,000           :'   LD HL,00A0   PITCH 160
150 DATA 001,003,000           :'   LD BC,0003   DURATION 3
160 DATA 205,092,052           :'   CALL 345C
170 DATA 225                   :'   POP HL
180 DATA 201                   :'   RET
190 POKE30862,241:POKE30863,143:'SET 788E/FH TO F1/8F FOR USR().
200 '*****MAIN MENU*****
205 'XXX LINES 220 TO 290 ARE INVERSE  XXX
210 CLS
220 PRINT:PRINT"        SOUND EFFECTS  ":PRINT
230 PRINT"A:DEC. ZOOP      B:INC. ZOOP     ";
240 PRINT"C:RANDOM BEEPS   D:WAVES         ";
250 PRINT"E:INC. PHASOR    F:DEC. PHASOR   ";
260 PRINT"G:UFO LEAVING    H:UFO LANDING   ";
270 PRINT"I:BUZZER         J:SHIP SIREN    ";
280 PRINT"K:BURGLAR ALARM  L:POLICE SIREN  ";
290 PRINT"M:TELEPHONE      X:EXIT          ";
320 PRINT@466,"     ";
330 PRINT@448,"ENTER OPTION#  ";:INPUT OP$
340 POKE DR%,3                  :'SET DURATION TO 3 ON ENTRY.
400 '*****BRANCH TO CHOICE*****
410 IF OP$="A",1100 ELSE IF OP$="B",1200
420 IF OP$="C",1300 ELSE IF OP$="D",1400
430 IF OP$="E",1500 ELSE IF OP$="F",1600
440 IF OP$="G",1700 ELSE IF OP$="H",1800
450 IF OP$="I",1900 ELSE IF OP$="J",2000
460 IF OP$="K",2100 ELSE IF OP$="L",2200
470 IF OP$="M",2300 ELSE IF OP$="X",10020
510 GOT0320
1100 '** ***DECAYING ZOOP*****
1110 CLS:PRINT@232,"DECAYING ZOOP"
1120 FOR T%=1 TO 255 STEP 4  :'LOWER PITCH - FIXED DURATION.
1130    POKE PT%,T%
1140    X=USR(0)
1150 NEXT T%
1160 GOTO 210
1200 '*****INCREASING ZOOP*****
1210 CLS:PRINT@232,"INCREASING ZOOP"
1220 FOR T%=255 TO 1 STEP -4 :'RAISE PITCH - FIXED DURATION.
1230    POKE PT%,T%
1240    X=USR(0)
1250 NEXT T%
1260 GOTO 210
1300 '*****RANDOM BEEPS*****
1310 CLS:PRINT@234,"RANDOM BEEPS"
1320 POKE DR%,10             :'CONSTANT DURATION.
1330 FOR Y%=1 TO 50          :'DO 50 REPETITIONS - VARY PITCH.
1340    T%=RND(254)+1
1350    POKE PT%,T%
1360    X=USR(0)
1370 NEXT Y%
1380 GOTO 210
1400 '*****WAVES*****
1410 CLS:PRINT@237,"WAVES"
1415 POKE DR%,1              :'FIX DURATION.
1420 FOR Y%=1 TO 10          :'DO 10 OSCILLATIONS.
1425    FOR T%=1 TO 10       :'LOWERING PITCH.
1430       POKE PT%,T%
1435       X=USR(0)
1440    NEXT T%
1445    FOR T%=30 TO 1 STEP -1 :'RAISING PITCH.
1450       POKE PT%,T%
1455       X=USR(0)
1460    NEXT T%
1465 NEXT Y%
1470 GOTO 210
1500 '*****INCREASING PHASOR*****
1510 CLS:PRINT@230,"INCREASING PHASOR"
1520 FOR Y%=20 TO 1 STEP -1  :'INCREASE STARTING PITCH.
1530    FOR T%=Y% TO 1 STEP -1 :'DECREASE NUMBER OF TONES.
1540       POKE PT%,T%
1550       X=USR(0)
1560    NEXT T%
1570 NEXT Y%
1580 GOTO 210
1600 '*****DECREASING PHASOR*****
1610 CLS:PRINT@230,"DECREASING PHASOR"
1620 FOR Y%=1 TO 20          :'DECREASE STARTING PITCH.
1630    FOR T%=1 TO Y%       :'INCREASE NUMBER OF TONES.
1640       POKE PT%,T%
1650       X=USR(0)
1660    NEXT T%
1670 NEXT Y%
1680 GOTO 210
1700 '*****UFO LEAVING*****
1710 CLS:PRINT@233,"UFO LEAVING"
1720 T%=61                   :'SET PITCH.
1730 FOR D%=60 TO 1 STEP -1  :'DECREASE DURATION.
1740    POKE DR%,D%
1750    POKE PT%,T%
1760    T%=T%-1              :'LOWER PITCH.
1770    X=USR(0)
1780 NEXT D%
1790 GOTO 210
1800 '*****UFO LANDING*****
1810 CLS:PRINT@233,"UFO LANDING"
1820 T%=1                    :'SET PITCH.
1830 FOR D%=1 TO 60          :'INCREASE DURATION.
1840    POKE DR%,D%
1850    POKE PT%,T%
1860    T%=T%+1              :'RAISE PITCH.
1870    X=USR(0)
1880 NEXT D%
1890 GOTO 210
1900 '*****BUZZER*****
1910 CLS:PRINT@236,"BUZZER"
1920 POKE DR%,3
1930 POKE PT%,60
1940 FOR Y%=1 TO 100         :'SOUND TONE 100 TIMES.
1950    X=USR(0)
1960       FOR D%=1 TO 5     :' SMALL DELAY.
1970       NEXT D%
1980 NEXT Y%
1990 GOTO 210
2000 '*****SHIP SIREN*****
2010 CLS:PRINT@234,"SHIP SIREN"
2015 POKE DR%,8              :'FIX DURATION.
2020 FOR Y%=1 TO 10          :'DO 10 REPEATS.
2025    FOR T%=200 TO 80 STEP -8:'INCREASE TONE.
2030       POKE PT%,T%
2035       X=USR(0)
2040    NEXT T%
2045    FOR D%=1 TO 150      :'DELAY BETWEEN REPEATS.
2050    NEXT D%
2055 NEXT Y%
2060 GOTO 210
2100 '*****BURGLAR ALARM*****
2110 CLS:PRINT@233,"BURGLAR ALARM"
2120 POKE DR%,255            :'FIX DURATION TO MAX.
2130 FOR Y%=1 TO 5           :'DO 5 CYCLES.
2140    POKE PT%,50          :'TONE HI.
2150    X=USR(0)
2160    POKE PT%,60          :'TONE LO.
2170    X=USR(0)
2180 NEXT Y%
2190 GOTO 210
2200 '*****POLICE SIREN*****
2210 CLS:PRINT@233,"POLICE SIREN"
2215 POKE DR%,9              :' FIX DURATION.
2220 FOR Y%=1 TO 5           :'DO 5 REPEATS.
2225    FOR T%=200 TO 80 STEP -4
2230       POKE PT%,T%       :'FAST RISING PITCH.
2235       X=USR(0)
2240    NEXT T%
2245    FOR T%=80 TO 200
2250       POKE PT%,T%       :'SLOW FALLING PITCH.
2255       X=USR(0)
2260    NEXT T%
2265 NEXT Y%
2270 GOTO 210
2300 '*****TELEPHONE*****
2310 CLS:PRINT@234,"TELEPHONE"
2315 POKE DR%,15             :'FIX DURATION.
2320 FOR Y%=1 TO 5           :'DO 5 REPEATS.
2325    FOR D%=1 TO 2        :'SOUND DOUBLE RING.
2330       FOR T%=1 TO 8     :'DO 8 WARBLES.
2335          POKE PT%,100   :'LO TONE.
2340          X=USR(0)
2345          POKE PT%,50    :'HI TONE.
2350          X=USR(0)
2355       NEXT T%
2360       FOR T%=1 TO 50     :'PAUSE BETWEEN DOUBLE RINGS.
2365       NEXT T%
2370    NEXT D%
2375    FOR D%=1 TO 400      :'PAUSE BETWEEN REPEATS.
2380    NEXT D%
2385 NEXT Y%
2390 GOTO 210
10000 CLS:PRINT"ERASING SOUNDS":ERA"SOUNDS"
10010 PRINT"SAVING SOUNDS":SAVE"SOUNDS"
10020 END


