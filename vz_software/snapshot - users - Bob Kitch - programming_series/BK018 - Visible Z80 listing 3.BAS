5 'LISTING 3 
10 '************************ 
20 '***   VISIBLE Z-80   *** 
30 '***   DEMO OF LDIR   *** 
40 '*** ASSEMBLER TUTOR  *** 
50 '*** BY R.B.K 7/3/91  *** 
60 '************************ 
70 '*** EXECUTION TIME 70 SECS. 
80 ' 
90 '***DEMONSTRATION OF SCREEN FILL IN BASIC THAT EMULATES 
100 '***THE LDIR INSTRUCTION OF THE Z80. 
110 '***VARIABLES USED RESEMBLE THOSE OF THE Z80 REGISTER SET. 
120 ' 
130 ' 
140 MODE(1)            :'***HI-RES SCREEN. 
150 COLOR ,0           :'***GREEN BACKGROUND. 
160 SOUND 10,1         :'***TIMING MARK. 
170 ' 
180 '***ASSEMBLER SIMULATION STARTS HERE. 
190 '***INITIALIZE ALL OF THE REGISTERS USED. 
200 A%=170             :'LD A,170 VALUE. 
210 HL%=28672          :'LD HL,7000H SOURCE. 
220 DE%=28673          :'LD DE,7001H DESTINATION. 
230 BC%=2047           :'LD BC,07FFH COUNT. 
280 ' 
290 '***PUTY FIRST VALUE INTO START OF VIDEO RAM. 
300 POKE HL%,A%        :'LD(HL),A 
380 ' 
390 '***CARRY OUT DESTRUCTIVE BLOCK MOVE. 
400 '                  :'LDIR 
410 POKE DE%,PEEK(HL%) :'(DE) <-(HL) 
420 HL%=HL%+1          :'HL <-HL+1 
430 DE%=DE%+1          :'DE <-DE+1 
440 BC%=BC%-1          :'BC <-BC-1 
450 IF BC%<>O THEN GOTO 410:' TEST FOR END 
460 '                  :'RET 
470 ' 
490 '***FINISH OFF. 
500 SOUND 10,1         :'***TIMING MARK. 
510 FOR I=0 TO 2000:NEXT I 
520 STOP 
600 END 
