
 5 OUT32,28:MODE(1):OUT32,29:MODE(1):OUT32,30:MODE(1)
 10 SP=2:O=0:P=0.5
 20 FORK=1TOSP:READC(K,1),C(K,2),C(K,3),T:R(K)=T:Q(K)=T*T:NEXTK
 30 DATA -0.3,-0.8,3,0.6
 40 DATA 0.9,-1.1,2,0.2
 50 FORI=0TO175:FORJ=0TO255
 70 X=0.3:Y=-0.5:Z=0:DX=J-128:DY=88-I
 72 DZ=300:DD=DX*DX+DY*DY+DZ*DZ
 100 N=-1:IFY<=OANDDY>O,N=0:S=-Y/DY
 110 FORK=1TOSP:PX=C(K,1)-X:PY=C(K,2)-Y:PZ=C(K,3)-Z
 140 SC=PX*DX+PY*DY+PZ*DZ
 150 IF SC<=O,GOTO 200
 155 PP=PX*PX+PY*PY+PZ*PZ
 160 BB=SC*SC/DD:AA=Q(K)-PP+BB
 180 IF AA<=O,GOTO 200
 190 SC=(SQR(BB)-SQR(AA))/SQR(DD):IFSC<SORN<O,N=K:S=SC
 200 NEXTK:IFN<0,350
 220 DX=DX*S:DY=DY*S:DZ=DZ*S:DD=DD*S*S:X=X+DX:Y=Y+DY:Z=Z+DZ
 240 IFN=O,300
 250 NX=X-C(N,1):NY=Y-C(N,2):NZ=Z-C(N,3)
 270 L=2*(DX*NX+DY*NY+DZ*NZ)/Q(N)
 280 DX=DX-NX*L:DY=DY-NY*L:DZ=DZ-NZ*L:GOTO100
 300 FORK=1TOSP:U=C(K,1)-X:V=C(K,3)-Z:IF U*U+V*V<=Q(K),GOTO350
 320 NEXT K
 330 IF((X-INT(X))>P)<>((Z-INT(Z))>P),GOSUB1000
 350 NEXTJ:NEXTI
 360 GOTO360
 1000 A=28672+INT(J/8)+32*(IAND63)
 1010 IF I < 192 OUT 32,30:IFI<128OUT32,29:IFI<64OUT32,28
 1020 POKE A,PEEK(A) OR 2^(7 AND NOT(J))
 1030 RETURN
